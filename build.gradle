plugins {
    id 'java'
    alias(libs.plugins.run.paper)
    alias(libs.plugins.spotless)
}

group = 'org.bysenom'
version = '1.0-SNAPSHOT'

// Version aus CI-Umgebung übernehmen, wenn Tag-Pattern vX.Y.Z
if (System.getenv("GITHUB_REF_NAME") != null) {
    def ref = System.getenv("GITHUB_REF_NAME")
    if (ref ==~ /v\d+\.\d+\.\d+/) {
        version = ref.substring(1)
        println "CI detected tag ${ref}, setting version to ${version}"
    }
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    // Paper API über Version Catalog
    compileOnly libs.paper.api
}

configurations.configureEach {
    resolutionStrategy {
        force libs.commons.lang3
    }
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(21)
    options.compilerArgs += ['-Xlint:deprecation', '-Xlint:unchecked']
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') { expand props }
}

// Reproducible builds
jar {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    manifest { attributes([
        'Implementation-Title': project.name,
        'Implementation-Version': project.version
    ]) }
}

// Run-Paper mit 1.21.4
runServer {
    minecraftVersion("1.21.4")
}

spotless {
    java {
        target 'src/**/*.java'
        importOrder()
        removeUnusedImports()
        endWithNewline()
    }
    format('misc') {
        target '*.md', '.editorconfig'
        trimTrailingWhitespace()
        endWithNewline()
    }
}
